locals {
  domain = "${var.subdomain_name}.${var.domain_name}"
}

data "cloudflare_zones" "domain" {
  filter {
    name = var.domain_name
  }
}

resource "aws_s3_bucket" "frontend" {
  bucket = local.domain
}

resource "aws_s3_bucket_public_access_block" "frontend" {
  bucket = aws_s3_bucket.frontend.id

  block_public_acls       = false
  block_public_policy     = false
  ignore_public_acls      = false
  restrict_public_buckets = false
}

resource "aws_s3_bucket_website_configuration" "frontend" {
  bucket = aws_s3_bucket.frontend.id

  index_document {
    suffix = "index.html"
  }
}

resource "aws_s3_bucket_policy" "frontend" {
  bucket = aws_s3_bucket.frontend.id

  policy = jsonencode({
    "Version" : "2012-10-17",
    "Statement" : [
      {
        "Sid" : "PublicReadGetObject",
        "Effect" : "Allow",
        "Principal" : "*",
        "Action" : "s3:GetObject",
        "Resource" : "arn:aws:s3:::${local.domain}/*",
        "Condition" : {
          "IpAddress" : {
            "aws:SourceIp" : [
              "2400:cb00::/32",
              "2405:8100::/32",
              "2405:b500::/32",
              "2606:4700::/32",
              "2803:f800::/32",
              "2c0f:f248::/32",
              "2a06:98c0::/29",
              "103.21.244.0/22",
              "103.22.200.0/22",
              "103.31.4.0/22",
              "104.16.0.0/12",
              "108.162.192.0/18",
              "131.0.72.0/22",
              "141.101.64.0/18",
              "162.158.0.0/15",
              "172.64.0.0/13",
              "173.245.48.0/20",
              "188.114.96.0/20",
              "190.93.240.0/20",
              "197.234.240.0/22",
              "198.41.128.0/17"
            ]
          }
        }
      }
    ]
  })
}

resource "cloudflare_record" "frontend_cname" {
  zone_id = data.cloudflare_zones.domain.zones[0].id
  name    = local.domain
  value   = aws_s3_bucket.frontend.website_endpoint
  type    = "CNAME"
  ttl     = 1
  proxied = true
}